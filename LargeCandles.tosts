#
# Large Candles
# v1.0.0
#
# This study highlights candles having a body-height the same or larger than ATR
# and larger than half of the entire range.
#
# @author: Andy Hudock <ahudock@pm.me>
#   based on Blake Young's large candle study
#

# Average length for determining large candles
input avg_length = 20;

# Minimum percentage ATR for large candles
input atr_factor = 0.8;

# Large bullish candle color
DefineGlobalColor("large uptick", Color.GREEN);

# Normal bullish candle color
DefineGlobalColor("normal uptick", Color.DARK_GREEN);

# Large bearish candle color
DefineGlobalColor("large downtick", Color.RED);

# Normal bearish candle color
DefineGlobalColor("normal downtick", Color.DARK_RED);

def candle_range        = high - low;
def is_bull_candle      = open < close;
def is_large_body       = BodyHeight() >= (candle_range * .5);
def is_avg_candle_size  = Average(candle_range, avg_length) * atr_factor;
def is_larger_than_avg  = candle_range >= is_avg_candle_size;
def is_large_candle     = is_large_body && is_larger_than_avg;

# Highlight large candles
AssignPriceColor(
    if is_large_candle && is_bull_candle then
        # Large bullish candle
        GlobalColor("large uptick")
    else if ! is_large_candle && is_bull_candle then
        # Normal bullish candle
        GlobalColor("normal uptick")
    else if is_large_candle && ! is_bull_candle then
        # Large bearish candle
        GlobalColor("large downtick")
    else
        # Normal bearish candle
        GlobalColor("normal downtick")
);
